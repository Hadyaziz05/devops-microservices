pipeline {
    agent {
        node {
            label 'jenkins-docker-agent'
        }
    }
    triggers {
        pollSCM 'H/5 * * * *'
    }
    environment {
        TAG = "v${env.BUILD_NUMBER}"
        IMAGE = "hadyaziz05/backend:${TAG}"
        DOCKER_HOST = 'tcp://docker-socat:2375'
    }

    stages {
        stage('Build') {
            steps {
                echo "Building Docker Image: ${IMAGE}"
                sh '''
                docker build -t $IMAGE backend/
                '''
            }
        }

        stage('Test') {
            steps {
                echo "Testing container..."
                sh '''
                docker run --rm $IMAGE node --version
                '''
            }
        }

        stage('Push') {
            steps {
                echo "Pushing to Docker Hub..."
                withCredentials([usernamePassword(credentialsId: 'dockerhub-creds', usernameVariable: 'USER', passwordVariable: 'PASS')]) {
                    sh '''
                    echo "$PASS" | docker login -u "$USER" --password-stdin
                    docker push $IMAGE
                    '''
                }
            }
        }

        stage('Deploy Blue-Green') {
            agent {
                label 'controller'
            }
            steps {
                echo "Starting Blue-Green Deployment..."
                withCredentials([file(credentialsId: 'kube-config-jenkins', variable: 'KUBECONFIG')]) {
                    script {
                        def currentVersion = sh(script: '''
                            kubectl get service backend-service -o=jsonpath='{.spec.selector.version}'
                        ''', returnStdout: true).trim()

                        def newVersion = currentVersion == "blue" ? "green" : "blue"
                        def newDeployment = "backend-${newVersion}"
                        def oldDeployment = "backend-${currentVersion}"

                        echo "Current version: ${currentVersion}"
                        echo "Deploying to: ${newDeployment} with image: ${IMAGE}"

                        //Step 1: Scale up the new deployment
                        sh """
                        kubectl scale deployment/${newDeployment} --replicas=2
                        """

                        //Step 2: Update new deployment with the new image
                        sh """
                        kubectl set image deployment/${newDeployment} backend=${IMAGE}
                        kubectl rollout status deployment/${newDeployment}
                        """

                        //Step 3: Switch service to point to new version
                        sh """
                        kubectl patch service backend-service -p '{"spec": {"selector": {"app": "backend", "version": "${newVersion}"}}}'
                        """

                        //Step 4: Scale down the old deployment
                        sh """
                        kubectl scale deployment/${oldDeployment} --replicas=0
                        """
                    }
                }
            }
        }


    }
}
